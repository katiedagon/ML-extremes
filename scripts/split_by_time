#!/bin/csh -f
#
if ("$#argv" == 0) then
   echo "Usage: split_by_time file"
   exit 1
else if ("$#argv" == 1) then
   set file = $argv[1]
else
   echo "Usage: split_by_time file"
   exit 1
endif
#
if !(-f ${file}) then
  echo "File ${file} does not exist. Error\!"
  exit 1
endif
#
# Example filename:  b.e13.BRCP85C5CN.ne120_g16.003.cam.h0.PS.207001-210012.nc
#
set prfx = `echo ${file} | cut -d"." -f1-8`
set ntp1 = `ncks --trd -F -H -C -v time ${file} | wc -l`
@ nt = $ntp1 - 1
#
# Step through time samples 12 at a time 
#
set it = 1
while ($it <= $nt)
  @ jt = $it + 11
  #
  # Extract out year from first four characters of "date"
  #
  set dt = `ncks -s  "%8.8i\n"  -F -H -C -v date -d time,${it},${it} ${file} | cut -c1-4`
  #
  set outfil = ${prfx}.${dt}01-${dt}12.nc
  echo ${outfil}" "${dt}" "${it}
  ncks -O -F -d time,${it},${jt} ${file} ${outfil}
  if ($status == 0) then
    @ it = $it + 12
  else
    echo "Error on ncks -O -F -d time,${it},${it} ${file} ${outfil}"
    exit 1
  endif
end
#
